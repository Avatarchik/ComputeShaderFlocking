// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

struct Node {

    float3 pos;
    float3 vel;
    float3 targetPosition;
    float mass;
    float maxSpeed;
    float maxTurnSpeed;
    float numberOfNodes;
    float obsCount;
};

struct Obstacle {

    float3 pos;
    float radius;

};

RWStructuredBuffer<Node> dataBuffer;
RWStructuredBuffer<Obstacle> obstacleBuffer;


// steer toward a target in 3d space
float3 SeekTarget ( Node node, float3 targetPosition )
{

    float3 desiredVelocity = targetPosition - node.pos;

    desiredVelocity = normalize(desiredVelocity) * node.maxSpeed;

    float3 turningForce = desiredVelocity - node.vel;
    if(length(turningForce) > node.maxTurnSpeed)
    {
        turningForce = normalize(turningForce) * node.maxTurnSpeed;
    }

    return turningForce;

}

// avoid a target in 3d space
float3 AvoidTarget ( Node node, float3 targetPosition )
{

    float3 desiredVelocity = node.pos - targetPosition;

    desiredVelocity = normalize(desiredVelocity) * node.maxSpeed;

    float3 turningForce = desiredVelocity - node.vel;

    if(length(turningForce) > node.maxTurnSpeed)
    {
        turningForce = normalize(turningForce) * node.maxTurnSpeed;
    }



    return turningForce;

}

float3 DistAvoidTarget( Node node, float3 targetPosition, float radius )
{

    float3 force = float3(0,0,0);

    if( length(targetPosition - node.pos) < radius ) force += AvoidTarget(node, targetPosition);

    return force;

}

// steer toward a target and decelerate when within distance [_arriveDistance]
float3 ArriveAtTarget ( Node node, float3 targetPosition )
{
    float3 desiredVelocity = targetPosition - node.pos;
    float dist = 1;

    if ( length(desiredVelocity) < dist )
    {
        desiredVelocity = normalize(desiredVelocity) * lerp( 0, node.maxSpeed, length(desiredVelocity) / 3 );
    }
    else
    {
        desiredVelocity = normalize(desiredVelocity) * node.maxSpeed;
    }

    if(length(desiredVelocity) > node.maxTurnSpeed)
    {
        desiredVelocity = normalize(desiredVelocity) * node.maxTurnSpeed;
    }

    return desiredVelocity;

}

// try to match neighbours' velocities within distance [app.alignment]
float3 AlignToNeighbours ( Node node, uint3 id )
{
    float3 combinedVelocities = float3(0,0,0);
    Node checkNode;
    int ct = 0;
    float dist = 1.5;

    [loop]
    for ( int i = 0; i < int(node.numberOfNodes); i++ )
    {
        if(i != int(id.x))
        {
            checkNode = dataBuffer[i];
            if(length(node.pos - checkNode.pos) < dist)
            {
                combinedVelocities += checkNode.vel;
                ct++;
            }
        }
    }

    if ( ct != 0 )
    {
        combinedVelocities = combinedVelocities / ct;
        if(length(combinedVelocities) > node.maxSpeed)
        {
            combinedVelocities = normalize(combinedVelocities) * node.maxSpeed;
        }
        if(length(combinedVelocities) > node.maxTurnSpeed)
        {
            combinedVelocities = normalize(combinedVelocities) * node.maxTurnSpeed;
        }

    }

    return combinedVelocities;
}

// try to find even spacing between neighbours within distance [app.cohesion]
float3 CohereToNeighbours ( Node node, uint3 id )
{
    float3 combinedPositions = float3(0,0,0);
    Node checkNode;
    int ct = 0;
    float dist = 1.0;

    [loop]
    for ( int i = 0; i < int(node.numberOfNodes); i++ )
    {
        if(i != int(id.x))
        {
            checkNode = dataBuffer[i];
            if(length(node.pos - checkNode.pos) < dist)
            {
                combinedPositions += checkNode.pos;
                ct++;
            }
        }
    }

    if ( ct != 0 )
    {
        //combinedPositions = SeekTarget( combinedPositions / ct );
        combinedPositions = ArriveAtTarget( node, combinedPositions / ct );
    }

    return combinedPositions;
}


float3 SeparateFromNeighbours( Node node, uint3 id )
{

    float3 force = float3(0,0,0);
    Node checkNode;
    float dist = 1.0;
    uint ct = 0;

    // loop through buffer, skip own ID
    [loop]
    for( int i = 0;i < int(node.numberOfNodes); i++ )
    {
        if(i != int(id.x))
        {
            checkNode = dataBuffer[i];

            //check distance
            if(length(node.pos - checkNode.pos) < dist)
            {

                force += normalize( node.pos - checkNode.pos ) / dist;
                ct++;

            }
        }
    }

    if(ct != 0)
    {
        force = normalize(force / ct) * node.maxSpeed;
        if(length(force) > node.maxTurnSpeed)
        {
            force = normalize(force) * node.maxTurnSpeed;
        }
    }

    return force;

}

float3 Flock( Node node, uint3 id )
{

    float3 flockForce = float3(0,0,0);

    flockForce += SeparateFromNeighbours(node, id);
    flockForce += AlignToNeighbours(node, id);
    flockForce += CohereToNeighbours(node, id);

    return flockForce;

}

[numthreads(96,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{

    // grab current node data from buffer
    Node node = dataBuffer[id.x];

    // create a new accel vector
    float3 accel = float3(0.0,0.0,0.0);

    // seek target first
    accel += SeekTarget( node, node.targetPosition );

    // add flocking behaviours
    accel += Flock( node, id );


    // add acceleration to velocity
    node.vel = node.vel + ( accel / node.mass);

    // avoid any obstacles
    if(node.obsCount > 0)
    {
        [loop]
        for(int i = 0; i < int(node.obsCount); i++)
        {
            //accel += DistAvoidTarget(node, obstacleBuffer[i].pos, obstacleBuffer[i].radius);
            node.vel += DistAvoidTarget(node, obstacleBuffer[i].pos, obstacleBuffer[i].radius);
        }
    }

    // cap speed
    if(length(node.vel) > node.maxSpeed)
    {
        node.vel = normalize(node.vel) * node.maxSpeed;
    }



    // add velocity to position
    node.pos += node.vel;

    // update buffer
    dataBuffer[id.x] = node;

}
